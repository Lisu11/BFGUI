
,>,>,                                 p1v{'0'_'9'}; p2v' '; p3v{'0'_'9'}
<[-]++++                              p2v4
[<------------>>------------<-]       p1v{0_9}; p2v0; p1v{0_9}; p2v0
<[->>+<<]                             p1v0; p3v{0_18}; p1

                            musimy zrobic kopie naszej sumy (w p10)
                            na wypadek gdyby nie byla parzysta 
>>[->>>>>>+>+<<<<<<<]             \ p9=p10=suma p3v0
>>>>>>[-<<<<<<+>>>>>>]            / p9v0 p3vSUMA

<<+              p7 znacznik zeby wejsc do petli gdy parzysta
<<<              p4
++++++++++<      do p4 add 10 zeby petla sie kiedys zatrzymala; p3

DZIELIMY na dwa
[--<<+>>>[->]<<]   co kazde odjeci dwoch odejmujemy 1 z p4 i dodajemy 1 
                   w p1 co bedzie wynikiem gdy liczba parzysta
                   Jesli liczba byla nieparzysta to ladujemy 
                   w p2 jesli parzysta wo w p3

>>>>             p7 V p6

[         jesli PARZYSTA to wynik juz mamy p7
    <<<<<

           do wyniku musimy dodac 48 i wyswielic potem wypisac 
           kropke i zero
    ++++++
    [-<++++++++>]
    <.
    [-]>++
    [-<+++++++++++++++++++++++>]
    <.++.

    [-]>[-]<   ta linjka zeruje dwie pierwsze komorki i nie wejdziemy
               do drugiej petli
]
>
[       jesli NIEPARZYSTA to wynik terzeba obliczyc
        suma byla kopiowana do p7 wiec musimy tam przejsc i odjac 1 
        zeby suma byla parzysta i zostanie nam jeszcze polowka

    >>>-       p7
    [-->+<] dzielenie na dwa

    WYSWIETLANIE WYNIKU z kropka 5
    ++++++
    [->++++++++<]
    >.
    [-]>++
    [-<+++++++++++++++++++++++>]
    <.+++++++.
    [-]  zeby wyjsc z petli
]